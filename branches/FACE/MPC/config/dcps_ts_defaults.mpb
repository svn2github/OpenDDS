// -*- MPC -*-
// $Id$

project : dds_macros {

  after += opendds_idl

  specific {
    opendds_idl = $(DDS_ROOT)/bin/opendds_idl
    opendds_idl_dep = $(DDS_ROOT)/bin/opendds_idl
  }

  Define_Custom(TypeSupport) {
    automatic_in          = 0
    automatic_out         = 1
    output_follows_input  = 0
    libpath               = $(ACE_ROOT)/lib
    command   = $(OPENDDS_IDL)
    dependent = $(OPENDDS_IDL_DEP) \
      $(DDS_ROOT)/dds/idl/CPPTemplate.txt \
      $(DDS_ROOT)/dds/idl/HTemplate.txt $(DDS_ROOT)/dds/idl/IDLTemplate.txt
    keyword dcps_ts_flags = commandflags
    inputext              = .idl
    pre_extension         = TypeSupportImpl
    source_outputext      = .cpp
    header_outputext      = .h

    // See $DDS_ROOT/MPC/modules/TYPESUPPORTHelper.pm for .idl and .java output
    // files and additional source and header files due to the -Gface option.
  }

}
